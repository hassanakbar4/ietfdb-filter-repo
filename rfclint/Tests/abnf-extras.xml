<rfc>
  <front>
    <title/>
    
    <author fullname="Jim Schaad" initials="J" surname="Schaad">
      <organization>IETF</organization>
    </author>
  </front>
  <middle>
    <section>
  <sourcecode type="abnf">
    <![CDATA[
cddl = S 1*rule
rule = typename [genericparm] S assign S type S
       / groupname [genericparm] S assign S grpent S

typename = id
groupname = id

assign = "=" / "/=" / "//="

genericparm = "<" S id S *("," S id S ) ">"
genericarg = "<" S type1 S *("," S type1 S ) ">"

]]>
  </sourcecode>
  <sourcecode type="abnf">
    <![CDATA[
type = type1 S *("/" S type1 S)

type1 = type2 [S (rangeop / ctlop) S type2]

type2 = value
         / typename [genericarg]
         / "(" type ")"
         / "~" S groupname [genericarg]
         / "#" "6" ["." uint] "(" S type S ")" ; note no space!
         / "#" DIGIT ["." uint]                ; major/ai
         / "#"                                 ; any
         / "{" S group S "}"
         / "[" S group S "]"
         / "&" S "(" S group S ")"
         / "&" S groupname [genericarg]

rangeop = "..." / ".."

ctlop = "." id

group = grpchoice S *("//" S grpchoice S)

grpchoice = *grpent

grpent = [occur S] [memberkey S] type optcom
       / [occur S] groupname [genericarg] optcom ; preempted by above
       / [occur S] "(" S group S ")" optcom

memberkey = type1 S "=>"
          / bareword S ":"
          / value S ":"

bareword = id

]]>
  </sourcecode>

  <sourcecode type="ASN.1">
    FOO ::= BEGIN END
  </sourcecode>
    </section>
  </middle>
</rfc>
